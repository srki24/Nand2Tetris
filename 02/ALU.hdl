// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    Mux16(a=x, b[0..15]=false, sel=zx, out= zxout);   // zero x/y
    Mux16(a=y, b[0..15]=false, sel=zy, out= zyout);

    Not16(in=zxout, out=notx);                        // not x/y
    Not16(in=zyout, out=noty);

    Mux16(a=zxout, b=notx, sel=nx, out=xin);              // chose neg x/y
    Mux16(a=zyout, b=noty, sel=ny, out=yin);


    Add16(a=xin, b=yin, out=add16out);                // Add
    And16(a=xin, b=yin, out=and16out);                // And 

    Mux16(a=and16out, b=add16out, sel=f, out=posout); // Chose and or add

    Not16(in=posout, out=negout);                     // Negate output

    Mux16(a=posout, b=negout, sel=no,                 // Chose output
                              out[0..7]=leftOut,
                              out[8..15]=rightOut,
                              out[15]=negBit,
                              out=out);                     
      

    And(a=negBit, b=negBit, out=ng);                    // ng (bit 15)


    Or8Way(in=leftOut, out=zrLeftOut);                // zr don't have or16way
    Or8Way(in=rightOut, out=zrRightOut);
    Or(a=zrLeftOut, b=zrRightOut, out=zout);
    Not(in=zout, out=zr);                                 

}